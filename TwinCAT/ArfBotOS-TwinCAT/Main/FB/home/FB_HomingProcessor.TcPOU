<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_HomingProcessor" Id="{6c30ff35-bc31-4ed8-ab4a-320ad080dc49}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_HomingProcessor
VAR CONSTANT
	NumberOfAxes: UINT:=GVL.NumberOfAxes;
END_VAR
VAR_IN_OUT
	ACS: ARRAY[1..NumberOfAxes] OF AXIS_REF;
	AxisGroup: AXES_GROUP_REF;
	GroupHandler: FB_GroupHandler;
	//Controller: FB_Controller;
END_VAR
VAR_INPUT
	Enable: BOOL;
	Reset: BOOL;
	StoredPositions: REFERENCE TO ARRAY[GVL.POS_ARR_BEG..GVL.POS_ARR_END] OF DUT_StoredPosition;
	VisionRegisters: REFERENCE TO ARRAY[GVL.VIS_ARR_BEG..GVL.VIS_ARR_END] OF DUT_VisionRegister;
	Flags: REFERENCE TO ARRAY[GVL.FLG_ARR_BEG..GVL.FLG_ARR_END] OF DUT_Flags;
END_VAR
VAR_OUTPUT
	Done: BOOL;
	Busy: BOOL;
	Error: BOOL;
	ErrorID: UDINT;
END_VAR
VAR
	_ExecuteRtrig: R_TRIG;
	_ExecuteFtrig: F_TRIG;
	_ResetRtrig: R_TRIG;
	_ErrorRtrig: R_TRIG;
	
	_SequenceMain: INT;
	_StartHoming: BOOL;
	
	_SMC_Homing_J1, _SMC_Homing_J2, _SMC_Homing_J3: MC_Home;
	_SMC_Homing_J4, _SMC_Homing_J5, _SMC_Homing_J6: MC_Home;
    _ST_HomingOptions_J1,_ST_HomingOptions_J2,_ST_HomingOptions_J3: ST_HomingOptions;
    _ST_HomingOptions_J4,_ST_HomingOptions_J5,_ST_HomingOptions_J6: ST_HomingOptions;
	
	_MC_SetPosition_J1, _MC_SetPosition_J2, _MC_SetPosition_J3: MC_SetPosition;
	_MC_SetPosition_J4, _MC_SetPosition_J5, _MC_SetPosition_J6: MC_SetPosition;
    _ST_SetPositionsOptions_J1,_ST_SetPositionsOptions_J2,_ST_SetPositionsOptions_J3: ST_SetPositionOptions;
    _ST_SetPositionsOptions_J4,_ST_SetPositionsOptions_J5,_ST_SetPositionsOptions_J6: ST_SetPositionOptions;
	
	_HomingVelocityFast: LREAL:= 10.0;
	_HomingVelocitySlow: LREAL:= 5.0;
	_HomingVelocitySlowSlow: LREAL:= 1.0;
	_HomingAcceleration: LREAL:= 1000.0;
	_HomingJerk: LREAL:= 10000.0;
	
	_MoveToPose: BOOL;
	_MoveCommand: ST_MoveCmd;
	_MoveCommandProcessor: MoveCmdProcessor;
	
	_MC_MoveAbsolute: ARRAY[1..NumberOfAxes] OF MC_MoveAbsolute;
	
	_i, _j, _k: UINT;
	_FirstScan: BOOL:=TRUE;
END_VAR

VAR CONSTANT
	STEP_DONE: 					INT := 9999;
	STEP_IDLE: 					INT := 0000;
	STEP_GROUP_ENABLE: 			INT := 0030;
	STEP_WAIT_ENABLE: 			INT := 0035;
	STEP_FAST_HOME_G1:			INT := 0040;
	STEP_WAIT_FAST_HOME_G1:		INT := 0045;
	STEP_SLOW_HOME_G1:			INT := 0050;
	STEP_WAIT_SLOW_HOME_G1:		INT := 0055;
	STEP_MOVE_HOME_G1:			INT := 0056;
	
	STEP_FAST_HOME_G2:			INT := 0060;
	STEP_WAIT_FAST_HOME_G2:		INT := 0065;
	STEP_SLOW_HOME_G2:			INT := 0070;
	STEP_WAIT_SLOW_HOME_G2:		INT := 0075;
	STEP_MOVE_HOME_G2:			INT := 0076;
	
	STEP_FAST_HOME_G3:			INT := 0080;
	STEP_WAIT_FAST_HOME_G3:		INT := 0085;
	STEP_SLOW_HOME_G3:			INT := 0090;
	STEP_WAIT_SLOW_HOME_G3:		INT := 0095;
	STEP_MOVE_HOME_G3:			INT := 0096;
	
	STEP_MOVE_TO_POSE:			INT := 1000;
	STEP_WAIT_MOVE_TO_POSE:		INT := 1010;
	STEP_WAIT_POSE_DONE:		INT := 1020;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[_ExecuteRtrig(CLK:=);
_ExecuteFtrig(CLK:=);
_ResetRtrig(CLK:=Reset);

IF _FirstScan THEN
	//
END_IF

CASE _SequenceMain OF
	STEP_DONE: // done
		Busy := FALSE;
		Done := TRUE;
		//_MoveCommandProcessor.M_Home();
		_SequenceMain := STEP_IDLE;
		
		_SMC_Homing_J1.Execute := FALSE;
		_SMC_Homing_J2.Execute := FALSE;
		_SMC_Homing_J3.Execute := FALSE;
		_SMC_Homing_J4.Execute := FALSE;
		_SMC_Homing_J5.Execute := FALSE;
		_SMC_Homing_J6.Execute := FALSE;
		
	STEP_IDLE: // IDLE
		IF _StartHoming AND Enable THEN
			_StartHoming := FALSE;
			Busy := TRUE;
			Done := FALSE;
			_SequenceMain := STEP_GROUP_ENABLE;
			Flags[GVL.HOME_COMPLETE].Value := FALSE;
		END_IF
		
	STEP_GROUP_ENABLE:
		IF GroupHandler.M_EnableGroup(DUT_CoordSystem.ACS) THEN
			_SequenceMain := STEP_WAIT_ENABLE;
		ELSE
			_SequenceMain := STEP_DONE;
			Error := TRUE;
			ErrorID := GroupHandler.ErrorID;
		END_IF
		
	STEP_WAIT_ENABLE:
		// TODO add timeout timer here
		IF GroupHandler.P_GroupEnabled THEN
			_SequenceMain := STEP_FAST_HOME_G1;
		END_IF;
		
	STEP_FAST_HOME_G1:	
		_SMC_Homing_J1.Execute := TRUE;
		_SMC_Homing_J2.Execute := TRUE;
		_SMC_Homing_J3.Execute := TRUE;
		_ST_HomingOptions_J1.SearchVelocity := _HomingVelocitySlow;
		_ST_HomingOptions_J2.SearchVelocity := _HomingVelocitySlow;
		_ST_HomingOptions_J3.SearchVelocity := _HomingVelocitySlow;
		_ST_HomingOptions_J1.SearchDirection := Tc2_MC2.MC_Direction.MC_Positive_Direction;
		_ST_HomingOptions_J2.SearchDirection := Tc2_MC2.MC_Direction.MC_Negative_Direction;
		_ST_HomingOptions_J3.SearchDirection := Tc2_MC2.MC_Direction.MC_Positive_Direction;
		_SequenceMain := STEP_WAIT_FAST_HOME_G1;
		
	STEP_WAIT_FAST_HOME_G1: // wait fast home group 1
		IF _SMC_Homing_J1.Done
			AND _SMC_Homing_J2.Done
			AND _SMC_Homing_J3.Done THEN
				_SMC_Homing_J1.Execute := FALSE;
				_SMC_Homing_J2.Execute := FALSE;
				_SMC_Homing_J3.Execute := FALSE;
				_SequenceMain := STEP_SLOW_HOME_G1;
		END_IF
		
	STEP_SLOW_HOME_G1: // slow home group 1
		_SMC_Homing_J1.Execute := TRUE;
		_SMC_Homing_J2.Execute := TRUE;
		_SMC_Homing_J3.Execute := TRUE;
		_ST_HomingOptions_J1.SearchVelocity := _HomingVelocitySlowSlow;
		_ST_HomingOptions_J2.SearchVelocity := _HomingVelocitySlow;
		_ST_HomingOptions_J3.SearchVelocity := _HomingVelocitySlowSlow;
		_SequenceMain := STEP_WAIT_SLOW_HOME_G1;
		
	STEP_WAIT_SLOW_HOME_G1:
		IF _SMC_Homing_J1.Done
			AND _SMC_Homing_J2.Done
			AND _SMC_Homing_J3.Done THEN
				_SMC_Homing_J1.Execute := FALSE;
				_SMC_Homing_J2.Execute := FALSE;
				_SMC_Homing_J3.Execute := FALSE;
				_MC_MoveAbsolute[1].Execute := FALSE;
				_MC_MoveAbsolute[2].Execute := FALSE;
				_MC_MoveAbsolute[3].Execute := FALSE;
				_SequenceMain := STEP_MOVE_HOME_G1;
		END_IF
		
	STEP_MOVE_HOME_G1:
		_MC_MoveAbsolute[1].Execute := TRUE;
		_MC_MoveAbsolute[2].Execute := TRUE;
		_MC_MoveAbsolute[3].Execute := TRUE;
		IF _MC_MoveAbsolute[1].Done 
			AND _MC_MoveAbsolute[2].Done 
			AND _MC_MoveAbsolute[3].Done THEN
				_SequenceMain := STEP_FAST_HOME_G2;
		END_IF
		
	STEP_FAST_HOME_G2:		
		_SMC_Homing_J4.Execute := TRUE;
		_SMC_Homing_J5.Execute := TRUE;
		_ST_HomingOptions_J2.SearchVelocity := _HomingVelocitySlow;
		_ST_HomingOptions_J3.SearchVelocity := _HomingVelocitySlow;
		_ST_HomingOptions_J4.SearchDirection := Tc2_MC2.MC_Direction.MC_Negative_Direction;
		_ST_HomingOptions_J5.SearchDirection := Tc2_MC2.MC_Direction.MC_Negative_Direction;
		_SequenceMain := STEP_WAIT_FAST_HOME_G2;
		
	STEP_WAIT_FAST_HOME_G2:
		IF _SMC_Homing_J4.Done
			AND _SMC_Homing_J5.Done THEN
				_SMC_Homing_J4.Execute := FALSE;
				_SMC_Homing_J5.Execute := FALSE;
				_SequenceMain := STEP_SLOW_HOME_G2;
		END_IF
		
	STEP_SLOW_HOME_G2:
		_SMC_Homing_J4.Execute := TRUE;
		_SMC_Homing_J5.Execute := TRUE;
		_ST_HomingOptions_J4.SearchVelocity := _HomingVelocitySlowSlow;
		_ST_HomingOptions_J5.SearchVelocity := _HomingVelocitySlowSlow;
		_SequenceMain := STEP_WAIT_SLOW_HOME_G2;
		
	STEP_WAIT_SLOW_HOME_G2:
		IF _SMC_Homing_J4.Done
			AND _SMC_Homing_J5.Done THEN
				_SMC_Homing_J4.Execute := FALSE;
				_SMC_Homing_J5.Execute := FALSE;
				_MC_MoveAbsolute[4].Execute := FALSE;
				_MC_MoveAbsolute[5].Execute := FALSE;
				_SequenceMain := STEP_MOVE_HOME_G2;
		END_IF
		
	STEP_MOVE_HOME_G2:
		_MC_MoveAbsolute[4].Execute := TRUE;
		_MC_MoveAbsolute[5].Execute := TRUE;
		IF _MC_MoveAbsolute[4].Done 
			AND _MC_MoveAbsolute[5].Done THEN
				_SequenceMain := STEP_FAST_HOME_G3;
		END_IF
		
	STEP_FAST_HOME_G3:
		_SMC_Homing_J6.Execute := TRUE;
		_ST_HomingOptions_J6.SearchVelocity := _HomingVelocitySlow;
		_ST_HomingOptions_J6.SearchDirection := Tc2_MC2.MC_Direction.MC_Positive_Direction;
		_SequenceMain := STEP_WAIT_FAST_HOME_G3;
		
	STEP_WAIT_FAST_HOME_G3:
		IF _SMC_Homing_J6.Done THEN
			_SMC_Homing_J6.Execute := FALSE;
			_SequenceMain := STEP_SLOW_HOME_G3;
		END_IF
		
	STEP_SLOW_HOME_G3:
		_SMC_Homing_J6.Execute := TRUE;
		_ST_HomingOptions_J6.SearchVelocity := _HomingVelocitySlowSlow;
		_SequenceMain := STEP_WAIT_SLOW_HOME_G3;
		
	STEP_WAIT_SLOW_HOME_G3:
		IF _SMC_Homing_J6.Done THEN
			_SMC_Homing_J6.Execute := FALSE;
			_MC_MoveAbsolute[6].Execute := FALSE;
			_SequenceMain := STEP_MOVE_HOME_G3;
		END_IF;
		
	STEP_MOVE_HOME_G3:
		_MC_MoveAbsolute[6].Execute := TRUE;
		IF _MC_MoveAbsolute[6].Done  THEN
			Flags[GVL.HOME_COMPLETE].Value := TRUE;
			IF NOT Flags[GVL.HOM_TO_POSE_IDX].Value THEN
				_SequenceMain := STEP_DONE;
			ELSE
				_SequenceMain := STEP_MOVE_TO_POSE;
			END_IF
		END_IF
		
	STEP_MOVE_TO_POSE:
		_MoveToPose := TRUE;
		_SequenceMain := STEP_WAIT_MOVE_TO_POSE;
	
	STEP_WAIT_MOVE_TO_POSE:
		IF _MoveCommandProcessor.ItfUnitMode.CurrentState <> PACK_ML.State.Idle THEN
			_SequenceMain := STEP_WAIT_POSE_DONE;
		END_IF
		
	STEP_WAIT_POSE_DONE:
		IF _MoveCommandProcessor.ItfUnitMode.CurrentState = PACK_ML.State.Idle THEN
			_SequenceMain := STEP_DONE;
		END_IF
	
END_CASE

_M_HandlePose();

//
IF _ResetRtrig.Q (*AND Error*) THEN
	Error := FALSE;
	ErrorID := 0;
	_SequenceMain := STEP_DONE;
	GroupHandler.M_ResetGroup();
	_MoveCommandProcessor.M_Home();
END_IF

//
_ErrorRtrig(CLK:=GroupHandler.Error
				OR _SMC_Homing_J1.Error
				OR _SMC_Homing_J2.Error
				OR _SMC_Homing_J3.Error
				OR _SMC_Homing_J4.Error
				OR _SMC_Homing_J5.Error
				OR _SMC_Homing_J6.Error
				OR _MoveCommandProcessor.Error);			
IF _ErrorRtrig.Q THEN
	Error := TRUE;
	IF GroupHandler.Error THEN
		ErrorID := GroupHandler.ErrorID;
	ELSIF _SMC_Homing_J1.Error THEN
		ErrorID := _SMC_Homing_J1.ErrorID;
	ELSIF _SMC_Homing_J2.Error THEN
		ErrorID := _SMC_Homing_J2.ErrorID;
	ELSIF _SMC_Homing_J3.Error THEN
		ErrorID := _SMC_Homing_J3.ErrorID;
	ELSIF _SMC_Homing_J4.Error THEN
		ErrorID := _SMC_Homing_J4.ErrorID;
	ELSIF _SMC_Homing_J5.Error THEN
		ErrorID := _SMC_Homing_J5.ErrorID;
	ELSIF _SMC_Homing_J6.Error THEN
		ErrorID := _SMC_Homing_J6.ErrorID;
	ELSIF _MoveCommandProcessor.Error THEN
		// TODO need to rework how error messages are handled (globally)
		// if the move command processor has an error, we dont know what it is.
	END_IF
END_IF

// https://infosys.beckhoff.com/content/1033/tcplclibmc2/458433931.html?id=4042515396221059782
//_ST_HomingOptions_J1.SearchDirection := MC_Negative_Direction;
_SMC_Homing_J1(
	Axis:= ACS[1], 
	Execute:= , 
	Position:=StoredPositions[GVL.HOM_POS_IDX].Position.X, // DEFAULT_HOME_POSITION, 
	HomingMode:= MC_HomingMode.MC_DefaultHoming, 
	BufferMode:= , 
	Options:= _ST_HomingOptions_J1, 
	bCalibrationCam:= , 
	Done=> , 
	Busy=> , 
	Active=> , 
	CommandAborted=> , 
	Error=> , 
	ErrorID=> );
_SMC_Homing_J2(
	Axis:= ACS[2], 
	Execute:= , 
	Position:=StoredPositions[GVL.HOM_POS_IDX].Position.Y, // DEFAULT_HOME_POSITION, 
	HomingMode:= MC_HomingMode.MC_DefaultHoming, 
	BufferMode:= , 
	Options:= _ST_HomingOptions_J2, 
	bCalibrationCam:= , 
	Done=> , 
	Busy=> , 
	Active=> , 
	CommandAborted=> , 
	Error=> , 
	ErrorID=> );
_SMC_Homing_J3(
	Axis:= ACS[3], 
	Execute:= , 
	Position:=StoredPositions[GVL.HOM_POS_IDX].Position.Z, // DEFAULT_HOME_POSITION, 
	HomingMode:= MC_HomingMode.MC_DefaultHoming, 
	BufferMode:= , 
	Options:= _ST_HomingOptions_J3, 
	bCalibrationCam:= , 
	Done=> , 
	Busy=> , 
	Active=> , 
	CommandAborted=> , 
	Error=> , 
	ErrorID=> );
_SMC_Homing_J4(
	Axis:= ACS[4], 
	Execute:= , 
	Position:=StoredPositions[GVL.HOM_POS_IDX].Position.A, // DEFAULT_HOME_POSITION, 
	HomingMode:= MC_HomingMode.MC_DefaultHoming, 
	BufferMode:= , 
	Options:= _ST_HomingOptions_J4, 
	bCalibrationCam:= , 
	Done=> , 
	Busy=> , 
	Active=> , 
	CommandAborted=> , 
	Error=> , 
	ErrorID=> );
_SMC_Homing_J5(
	Axis:= ACS[5], 
	Execute:= , 
	Position:=StoredPositions[GVL.HOM_POS_IDX].Position.B, // DEFAULT_HOME_POSITION, 
	HomingMode:= MC_HomingMode.MC_DefaultHoming, 
	BufferMode:= , 
	Options:= _ST_HomingOptions_J5, 
	bCalibrationCam:= , 
	Done=> , 
	Busy=> , 
	Active=> , 
	CommandAborted=> , 
	Error=> , 
	ErrorID=> );
_SMC_Homing_J6(
	Axis:= ACS[6], 
	Execute:= , 
	Position:=StoredPositions[GVL.HOM_POS_IDX].Position.C, // DEFAULT_HOME_POSITION, 
	HomingMode:= MC_HomingMode.MC_DefaultHoming, 
	BufferMode:= , 
	Options:= _ST_HomingOptions_J6, 
	bCalibrationCam:= , 
	Done=> , 
	Busy=> , 
	Active=> , 
	CommandAborted=> , 
	Error=> , 
	ErrorID=> );
	
//
_ST_SetPositionsOptions_J1.ClearPositionLag := TRUE;
_MC_SetPosition_J1(
	Axis:= ACS[1], 
	Execute:= _MC_SetPosition_J1.Execute AND NOT _MC_SetPosition_J1.Done AND NOT _MC_SetPosition_J1.Error, 
	Position:= 0.0, 
	Mode:= FALSE, // true=relative false=absolute
    Options:= _ST_SetPositionsOptions_J1,
	Done=> , 
	Busy=> , 
	Error=> , 
	ErrorID=> );
_ST_SetPositionsOptions_J2.ClearPositionLag := TRUE;
_MC_SetPosition_J2(
	Axis:= ACS[2], 
	Execute:= _MC_SetPosition_J2.Execute AND NOT _MC_SetPosition_J2.Done AND NOT _MC_SetPosition_J2.Error, 
	Position:= 0.0, 
	Mode:= FALSE, // true=relative false=absolute
    Options:= _ST_SetPositionsOptions_J2,
	Done=> , 
	Busy=> , 
	Error=> , 
	ErrorID=> );
_ST_SetPositionsOptions_J3.ClearPositionLag := TRUE;
_MC_SetPosition_J3(
	Axis:= ACS[3], 
	Execute:= _MC_SetPosition_J3.Execute AND NOT _MC_SetPosition_J3.Done AND NOT _MC_SetPosition_J3.Error, 
	Position:= 0.0, 
	Mode:= FALSE, // true=relative false=absolute
    Options:= _ST_SetPositionsOptions_J3,
	Done=> , 
	Busy=> , 
	Error=> , 
	ErrorID=> );
_ST_SetPositionsOptions_J4.ClearPositionLag := TRUE;
_MC_SetPosition_J4(
	Axis:= ACS[4], 
	Execute:= _MC_SetPosition_J4.Execute AND NOT _MC_SetPosition_J4.Done AND NOT _MC_SetPosition_J4.Error, 
	Position:= 0.0, 
	Mode:= FALSE, // true=relative false=absolute
    Options:= _ST_SetPositionsOptions_J4,
	Done=> , 
	Busy=> , 
	Error=> , 
	ErrorID=> );
_ST_SetPositionsOptions_J5.ClearPositionLag := TRUE;
_MC_SetPosition_J5(
	Axis:= ACS[5], 
	Execute:= _MC_SetPosition_J5.Execute AND NOT _MC_SetPosition_J5.Done AND NOT _MC_SetPosition_J5.Error, 
	Position:= 0.0, 
	Mode:= FALSE, // true=relative false=absolute
    Options:= _ST_SetPositionsOptions_J5,
	Done=> , 
	Busy=> , 
	Error=> , 
	ErrorID=> );
_ST_SetPositionsOptions_J6.ClearPositionLag := TRUE;
_MC_SetPosition_J6(
	Axis:= ACS[6], 
	Execute:= _MC_SetPosition_J6.Execute AND NOT _MC_SetPosition_J6.Done AND NOT _MC_SetPosition_J6.Error, 
	Position:= 0.0, 
	Mode:= FALSE, // true=relative false=absolute
    Options:= _ST_SetPositionsOptions_J6,
	Done=> , 
	Busy=> , 
	Error=> , 
	ErrorID=> );
	
FOR _k:= 1 TO NumberOfAxes DO
	_MC_MoveAbsolute[_k](
		Axis:= ACS[_k], 
		Execute:= , 
		Position:= 0.0, 
		Velocity:= _HomingVelocityFast, 
		Acceleration:= MC_DEFAULT, 
		Deceleration:= MC_DEFAULT, 
		Jerk:= MC_DEFAULT, 
		BufferMode:= , 
		Options:= , 
		Done=> , 
		Busy=> , 
		Active=> , 
		CommandAborted=> , 
		Error=> , 
		ErrorID=> );
END_FOR]]></ST>
    </Implementation>
    <Folder Name="private" Id="{e1ed6b77-9798-48c9-8936-2629def057bb}" />
    <Folder Name="public" Id="{d78dbefb-b47c-4f80-bc4e-2ff84a391da0}" />
    <Method Name="_M_HandlePose" Id="{8dbad245-108d-44dc-844d-47ebe55e9287}" FolderPath="private\">
      <Declaration><![CDATA[METHOD PRIVATE _M_HandlePose : BOOL
VAR
	_MoveCommand: ST_MoveCmd;
	_PoseSpeed: LREAL := 20.0;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _MoveToPose THEN
	_MoveToPose:= FALSE;
	//
	IF GVL.PowerOn 
		AND_THEN Flags[GVL.HOME_COMPLETE].Value
		AND_THEN NOT _MoveCommandProcessor.Busy
		AND_THEN NOT _MoveCommandProcessor.Error THEN
			//
			_MoveCommand.MoveType 			:= DUT_Command_MoveType.DirectMove;
			_MoveCommand.PositionType 		:= DUT_Command_PositionType.AbsolutePosition;
			_MoveCommand.CoordinateType 	:= DUT_Command_CoordinateType.Literal;
			_MoveCommand.Speed 				:= _PoseSpeed;
			_MoveCommand.CoordSystem 		:= DUT_CoordSystem.ACS;
			_MoveCommand.CoordRef 			:= StoredPositions[GVL.POSE_2_IDX].Position;
			//
			_MoveCommandProcessor.Command := BuildMoveCmd(_MoveCommand);
			_MoveCommandProcessor.M_Start();
	END_IF;
END_IF

_MoveCommandProcessor(
	StoredPositions:= StoredPositions,
	VisionRegisters:= VisionRegisters,
	Flags:= Flags,
	AxisGroup:= AxisGroup, 
	GroupHandler:= GroupHandler,
	Velocity:= StoredPositions[GVL.SPEEDS_IDX].Position.X, 
	VelFactor:= StoredPositions[GVL.SPEEDS_IDX].Position.A,
	Acceleration:= StoredPositions[GVL.SPEEDS_IDX].Position.Y, 
	AccFactor:= StoredPositions[GVL.SPEEDS_IDX].Position.B,
	Jerk:= StoredPositions[GVL.SPEEDS_IDX].Position.Z,
	JerkFactor:= StoredPositions[GVL.SPEEDS_IDX].Position.C, 
	Position=> ,
	ItfUnitMode=> , 
	Error=> );]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetHome" Id="{ac8ad3ff-e2e3-4571-99f3-004f0df0c316}" FolderPath="public\">
      <Declaration><![CDATA[METHOD M_SetHome : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
// adjust all the persistent variables to take on the calibrated values
StoredPositions[GVL.HOM_POS_IDX].Position.X := F_HomeOffsetCalc(	CurrentOffset:= PersistentVars.StoredPositions[GVL.HOM_POS_IDX].Position.X, 
																				CurrentPosition:= ACS[1].NcToPlc.ActPos, 
																				HomeSearchDirection:= _ST_HomingOptions_J1.SearchDirection);

StoredPositions[GVL.HOM_POS_IDX].Position.Y := F_HomeOffsetCalc(	CurrentOffset:= PersistentVars.StoredPositions[GVL.HOM_POS_IDX].Position.Y, 
																				CurrentPosition:= ACS[2].NcToPlc.ActPos, 
																				HomeSearchDirection:= _ST_HomingOptions_J2.SearchDirection);

StoredPositions[GVL.HOM_POS_IDX].Position.Z := F_HomeOffsetCalc(	CurrentOffset:= PersistentVars.StoredPositions[GVL.HOM_POS_IDX].Position.Z, 
																				CurrentPosition:= ACS[3].NcToPlc.ActPos, 
																				HomeSearchDirection:= _ST_HomingOptions_J3.SearchDirection);

StoredPositions[GVL.HOM_POS_IDX].Position.A := F_HomeOffsetCalc(	CurrentOffset:= PersistentVars.StoredPositions[GVL.HOM_POS_IDX].Position.A, 
																				CurrentPosition:= ACS[4].NcToPlc.ActPos, 
																				HomeSearchDirection:= _ST_HomingOptions_J4.SearchDirection);

StoredPositions[GVL.HOM_POS_IDX].Position.B := F_HomeOffsetCalc(	CurrentOffset:= PersistentVars.StoredPositions[GVL.HOM_POS_IDX].Position.B, 
																				CurrentPosition:= ACS[5].NcToPlc.ActPos, 
																				HomeSearchDirection:= _ST_HomingOptions_J5.SearchDirection);

StoredPositions[GVL.HOM_POS_IDX].Position.C := F_HomeOffsetCalc(	CurrentOffset:= PersistentVars.StoredPositions[GVL.HOM_POS_IDX].Position.C, 
																				CurrentPosition:= ACS[6].NcToPlc.ActPos, 
																				HomeSearchDirection:= _ST_HomingOptions_J6.SearchDirection);

// this isnt totally needed but will calibrate the current session without the need to rehome
_MC_SetPosition_J1.Execute:=TRUE;
_MC_SetPosition_J2.Execute:=TRUE;
_MC_SetPosition_J3.Execute:=TRUE;
_MC_SetPosition_J4.Execute:=TRUE;
_MC_SetPosition_J5.Execute:=TRUE;
_MC_SetPosition_J6.Execute:=TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StartHoming" Id="{1ff714ca-aba4-448a-bc01-bf0eab578f27}" FolderPath="public\">
      <Declaration><![CDATA[METHOD M_StartHoming : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF _SequenceMain = STEP_IDLE THEN
	M_StartHoming := TRUE;
	_StartHoming := TRUE;
ELSE
	M_StartHoming := FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>